/*
 * Copyright (c) 2015 RWTH Aachen. All rights reserved.
 *
 * http://www.se-rwth.de/
 */
package org.nest;

/**
  Grammar representing the Simple Programming Language (SPL). It is easy to learn imperative
  language which leans on the Python syntax.
*/
grammar NESTML extends org.nest.Literals {

  /** ASTNESTMLCompilationUnit represents the complete entire file with neuron and component models.
      @attribute Neuron   The neuron representation
    */
  NESTMLCompilationUnit = (Neuron | NEWLINE)* EOF;

  BLOCK_OPEN = ":";

  BLOCK_CLOSE = "end";

  /*********************************************************************************************************************
  * Units-Language
  *********************************************************************************************************************/

  /**
    ASTDatatype. Represents predefined datatypes and gives a possibility to use an unit
    datatype.
    @attribute boolean getters for integer, real, ...
    @attribute unitType a SI datatype
  */
  Datatype = ["integer"]
           | ["real"]
           | ["string"]
           | ["boolean"]
           | ["void"]
           | UnitType;

  /**
    ASTUnitType. Represents an unit datatype. It can be a plain datatype as 'mV' or a
    complex data type as 'mV/s'
  */
  UnitType = leftParentheses:"(" UnitType rightParentheses:")"
           | base:UnitType pow:["**"] exponent:IntLiteral
           | left:UnitType (timesOp:["*"] | divOp:["/"]) right:UnitType
           | unitlessLiteral:IntLiteral divOp:["/"] right:UnitType
           | unit:Name;

  /*********************************************************************************************************************
  * Expressions-Language
  *********************************************************************************************************************/
  Expr = leftParentheses:["("] Expr rightParentheses:[")"]
       | <rightassoc> base:Expr pow:["**"] exponent:Expr
       | (unaryPlus:["+"] | unaryMinus:["-"] | unaryTilde:["~"]) term:Expr
       | left:Expr (timesOp:["*"] | divOp:["/"] | moduloOp:["%"]) right:Expr
       | left:Expr (plusOp:["+"] | minusOp:["-"]) right:Expr
       | left:Expr (shiftLeft:["<<"] | shiftRight:[">>"]) right:Expr
       | left:Expr bitAnd:["&"] right:Expr
       | left:Expr bitXor:["^"]  right:Expr
       | left:Expr bitOr:["|"] right:Expr
       | left:Expr (lt:["<"] |
                    le:["<="] |
                    eq:["=="] |
                    ne:["!="] |
                    ne2:["<>"] |
                    ge:[">="] |
                    gt:[">"]) right:Expr
       | logicalNot:["not"] Expr
       | left:Expr logicalAnd:["and"] right:Expr
       | left:Expr logicalOr:["or"] right:Expr
       | condition:Expr "?" ifTrue:Expr ":" ifNot:Expr
       | FunctionCall
       | BooleanLiteral // true & false;
       | NumericLiteral Variable
       | NumericLiteral
       | StringLiteral
       | ["inf"]
       | Variable;

  /**
    ASTVariable Provides a 'marker' AST node to identify variables used in expressions.
    @attribute name
  */
  Variable = Name (differentialOrder:"\'")*;

  /**
    ASTFunctionCall Represents a function call, e.g. myFun("a", "b").
    @attribute name The (qualified) name of the fucntions
    @attribute args Comma separated list of expressions representing parameters.
  */
  FunctionCall = calleeName:Name "(" args:(Expr& || ",")* ")";


  /*********************************************************************************************************************
  * Equations-Language
  *********************************************************************************************************************/
  OdeFunction = ([recordable:"recordable"])? "function" variableName:Name Datatype "=" Expr (";")?;

  /** ASTeq Represents an equation, e.g. "I = exp(t)" or represents an differential equations, e.g. "V_m' = V_m+1".
    @attribute lhs      Left hand side, e.g. a Variable.
    @attribute rhs      Expression defining the right hand side.
  */
  Equation =  lhs:Derivative "=" rhs:Expr (";")?;

  Derivative = name:Name (differentialOrder:"\'")*;

  Shape = "shape" lhs:Derivative "=" rhs:Expr (";")?;

  /*********************************************************************************************************************
  * Procedural-Language
  *********************************************************************************************************************/
  Block = ( Stmt | NEWLINE )*;

  Stmt = Small_Stmt | Compound_Stmt;

  Compound_Stmt = IF_Stmt
                | FOR_Stmt
                | WHILE_Stmt;

  Small_Stmt = Assignment
             | FunctionCall
             | Declaration
             | ReturnStmt;

  Assignment = lhsVarialbe:Variable
    (assignment:["="]       |
     compoundSum:["+="]     |
     compoundMinus:["-="]   |
     compoundProduct:["*="] |
     compoundQuotient:["/="]) Expr;

  /** ASTDeclaration A variable declaration. It can be a simple declaration defining one or multiple variables:
   'a,b,c real = 0'. Or an function declaration 'function a = b + c'.
    @attribute hide is true iff. declaration is not trackable.
    @attribute function is true iff. declaration is an function.
    @attribute vars          List with variables
    @attribute Datatype      Obligatory data type, e.g. 'real' or 'mV/s'
    @attribute sizeParameter An optional array parameter. E.g. 'tau_syn ms[n_receptros]'
    @attribute expr An optional initial expression, e.g. 'a real = 10+10'
    @attribute invariants List with optional invariants.
   */
  Declaration =
    (["recordable"] )? (["function"])?
    vars:Variable ("," vars:Variable)*
    Datatype
    ("[" sizeParameter:Name "]")?
    ( "=" Expr)?
    ("[[" invariant:Expr "]]")?;

  /** ATReturnStmt Models the return statement in a function.

    @attribute minus An optional sing
    @attribute definingVariable Name of the variable
   */
  ReturnStmt = "return" Expr?;

  IF_Stmt = IF_Clause
            ELIF_Clause*
            (ELSE_Clause)?
            BLOCK_CLOSE;

  IF_Clause = "if" Expr BLOCK_OPEN Block;

  ELIF_Clause = "elif" Expr BLOCK_OPEN Block;

  ELSE_Clause = "else" BLOCK_OPEN Block;

  FOR_Stmt = "for" var:Name "in" from:Expr "..." to:Expr "step" step:SignedNumericLiteral BLOCK_OPEN Block BLOCK_CLOSE;

  WHILE_Stmt = "while" Expr BLOCK_OPEN Block BLOCK_CLOSE;

  /*********************************************************************************************************************
  * Nestml-Language
  *********************************************************************************************************************/
  /** ASTNeuron represents neuron.
    @attribute Name    The name of the neuron
    @attribute Body    The body of the neuron, e.g. internal, state, parameter...
  */
  Neuron = "neuron" Name
    BLOCK_OPEN
      ( NEWLINE |
        BlockWithVariables |
        UpdateBlock |
        EquationsBlock |
        InputBlock |
        OutputBlock |
        Function)*
    BLOCK_CLOSE;

  /** ASTBlockWithVariables represent a block with variables, e.g.:
    state:
      y0, y1, y2, y3 mV [y1 > 0; y2 > 0]
    end

    @attribute state true if the varblock ist a state.
    @attribute parameter true if the varblock ist a parameter.
    @attribute internal true if the varblock ist a state internal.
    @attribute AliasDecl a list with variable declarations.
  */
  BlockWithVariables =
    (["state"] | ["parameters"] | ["internals"] | ["initial_values"])
    BLOCK_OPEN
      (Declaration | NEWLINE)*
    BLOCK_CLOSE;

  /** ASTDynamics a special function definition:
      update:
        if r == 0: # not refractory
          integrate(V)
        end
      end
     @attribute block Implementation of the dynamics.
   */
  UpdateBlock =
    "update"
    BLOCK_OPEN
      Block
    BLOCK_CLOSE;

  /** ASTEquations a special function definition:
       equations:
         G = (e/tau_syn) * t * exp(-1/tau_syn*t)
         V' = -1/Tau * V + 1/C_m * (I_sum(G, spikes) + I_e + currents)
       end
     @attribute odeDeclaration Block with equations and differential equations.
   */
  EquationsBlock =
    "equations"
    BLOCK_OPEN
      (Equation | Shape | OdeFunction | NEWLINE)*
    BLOCK_CLOSE;

  /** ASTInput represents the input block:
  input:
    spikeBuffer   <- inhibitory excitatory spike
    currentBuffer <- current
  end

    @attribute inputLine set of input lines.
  */
  InputBlock = "input"
    BLOCK_OPEN
      (InputLine | NEWLINE)*
    BLOCK_CLOSE;

  /** ASTInputLine represents a single line form the input, e.g.:
      spikeBuffer   <- inhibitory excitatory spike

    @attribute sizeParameter Optional parameter representing  multisynapse neuron.
    @attribute sizeParameter Type of the inputchannel: e.g. inhibitory or excitatory (or both).
    @attribute spike true iff the neuron is a spike.
    @attribute current true iff. the neuron is a current.
  */
  InputLine = Name ("[" sizeParameter:Name "]")? "<-" InputType* (["spike"] | ["current"]);

  /** ASTInputType represents the type of the inputline e.g.: inhibitory or excitatory:
    @attribute inhibitory true iff the neuron is a inhibitory.
    @attribute excitatory true iff. the neuron is a excitatory.
  */
  InputType = (["inhibitory"] | ["excitatory"]);

  /** ASTOutput represents the output block of the neuron:
        output: spike
      @attribute spike true iff the neuron has a spike output.
      @attribute current true iff. the neuron is a current output.
    */
  OutputBlock = "output" BLOCK_OPEN (["spike"] | ["current"]) ;

  /** ASTFunction a function definition:
      function set_V_m(v mV):
        y3 = v - E_L
      end
    @attribute name Functionname.
    @attribute parameters List with function parameters.
    @attribute returnType Complex return type, e.g. String
    @attribute primitiveType Primitive return type, e.g. int
    @attribute block Implementation of the function.
  */
  Function = "function" Name "(" (Parameter ("," Parameter)*)? ")" (returnType:Datatype)?
           BLOCK_OPEN
             Block
           BLOCK_CLOSE;

  /** ASTParameter represents singe:
      output: spike
    @attribute compartments Lists with compartments.
  */
  Parameter = Name Datatype;
}
