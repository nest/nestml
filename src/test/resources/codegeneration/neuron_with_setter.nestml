/*
Name: iaf_psc_alpha - Leaky integrate-and-fire neuron model.

Description:

  iaf_psc_alpha is an implementation of a leaky integrate-and-fire model
  with alpha-function shaped synaptic currents. Thus, synaptic currents
  and the resulting post-synaptic potentials have a finite rise time.

  The threshold crossing is followed by an absolute refractory period
  during which the membrane potential is clamped to the resting potential.

  The linear subthresold dynamics is integrated by the Exact
  Integration scheme [1]. The neuron dynamics is solved on the time
  grid given by the computation step size. Incoming as well as emitted
  spikes are forced to that grid.

  An additional state variable and the corresponding differential
  equation represents a piecewise constant external current.

  The general framework for the consistent formulation of systems with
  neuron like dynamics interacting by point events is described in
  [1].  A flow chart can be found in [2].

  Critical tests for the formulation of the neuron model are the
  comparisons of simulation results for different computation step
  sizes. sli/testsuite/nest contains a number of such tests.

  The iaf_psc_alpha is the standard model used to check the consistency
  of the nest simulation kernel because it is at the same time complex
  enough to exhibit non-trivial dynamics and simple enough compute
  relevant measures analytically.

Remarks:

  The present implementation uses individual variables for the
  components of the state vector and the non-zero matrix elements of
  the propagator.  Because the propagator is a lower triangular matrix
  no full matrix multiplication needs to be carried out and the
  computation can be done "in place" i.e. no temporary state vector
  object is required.

  The template support of recent C++ compilers enables a more succinct
  formulation without loss of runtime performance already at minimal
  optimization levels. A future version of iaf_psc_alpha will probably
  address the problem of efficient usage of appropriate vector and
  matrix objects.

Remarks:

  If tau_m is very close to tau_syn_ex or tau_syn_in, the model
  will numerically behave as if tau_m is equal to tau_syn_ex or
  tau_syn_in, respectively, to avoid numerical instabilities.
  For details, please see IAF_Neruons_Singularity.ipynb in
  the NEST source code (docs/model_details).

References:
  [1] Rotter S & Diesmann M (1999) Exact simulation of time-invariant linear
      systems with applications to neuronal modeling. Biologial Cybernetics
      81:381-402.
  [2] Diesmann M, Gewaltig M-O, Rotter S, & Aertsen A (2001) State space
      analysis of synchronous spiking in cortical neural networks.
      Neurocomputing 38-40:565-571.
  [3] Morrison A, Straube S, Plesser H E, & Diesmann M (2006) Exact subthreshold
      integration with continuous spike times in discrete time neural network
      simulations. Neural Computation, in press

Sends: SpikeEvent

Receives: SpikeEvent, CurrentEvent, DataLoggingRequest
FirstVersion: September 1999
Author:  Diesmann, Gewaltig
SeeAlso: iaf_psc_delta, iaf_psc_exp, iaf_cond_exp
*/
neuron iaf_psc_alpha_neuron:

  state:
    V_abs mV
    alias V_m mV = V_abs + E_L # Membrane potential.
  end

  function set_V_m(V_m mV):
    V_abs = V_m + V_abs
  end

  equations:
    shape I_shape_in = (e/tau_syn_in) * t * exp(-1/tau_syn_in*t)
    shape I_shape_ex = (e/tau_syn_ex) * t * exp(-1/tau_syn_ex*t)

    V_abs' = -1/Tau * V_abs + 1/C_m * (curr_sum(I_shape_in, in_spikes) + curr_sum(I_shape_ex, ex_spikes) + I_e + currents)
  end

  parameters:
    C_m     pF = 250pF   # Capacity of the membrane
    Tau     ms = 10ms    # Membrane time constant.
    tau_syn_in ms = 2ms  # Time constant of synaptic current.
    tau_syn_ex ms = 2ms  # Time constant of synaptic current.
    t_ref   ms = 2ms     # Duration of refractory period in ms.
    E_L     mV = -70mV   # Resting potential.
    alias V_reset mV = -70mV - E_L # Reset potential of the membrane in mV.
    alias Theta   mV = -55mV - E_L # Spike threshold in mV.
    I_e     pA = 0pA     # Constant external input current in pA.
  end

  internals:
    RefractoryCounts integer = steps(t_ref) # refractory time in steps
    r integer                               # counts number of tick during the refractory period
  end

  input:
    ex_spikes   <- excitatory spike
    in_spikes   <- inhibitory spike
    currents    <- current
  end

  output: spike

  update:
    if r == 0: # neuron not refractory
      integrate_odes()
    else: # neuron is absolute refractory
      r = r - 1
    end

    if V_abs >= Theta: # threshold crossing
      # A supra-threshold membrane potential should never be observable.
      # The reset at the time of threshold crossing enables accurate
      # integration independent of the computation step size, see [2,3] for
      # details.
      r = RefractoryCounts
      V_abs = V_reset
      emit_spike()
    end

  end

end
