neuron iaf_neuron_ode:

  state
    V mV
    alias V_m mV = V + E_L # Membrane potenCodetial.
  end

  equations:
      shape G = (e/tau_syn) * t * exp(-1/tau_syn*t)
      V' = -1/Tau * V + 1/C_m * (G + I_e + currents)
  end

  parameters:
    # Capacity of the membrane
    C_m       pF = 250pF
    # Membrane time constant.
    Tau     ms = 10
    # Time constant of synaptic current.
    tau_syn ms = 2
    # Refractory period.
    t_ref    ms = 2
    # Resting potential.
    E_L      mV = -70
    delta_V_reset mV = -70 - E_L
    Theta   mV = -55 - E_L
    # External current.
    I_e     pA = 0 # special
    # some aliases Threshold.
    alias V_th mV = Theta + E_L
    # Reset value of the membrane potential.
    alias  V_reset mV = delta_V_reset + E_L
    # invariant V_reset >= Theta
  end

  internals:
    h   ms   = resolution()
    r  integer
    RefractoryCounts integer = steps(t_ref)
  end

  input:
    spikes   <- inhibitory excitatory spike
    currents <- current
  end

  output: spike

  update:

    if r == 0: # not refractory
        integrate_odes()
    else:
        r = r - 1
    end

    # threshold crossing
    if V >= Theta:
        r = RefractoryCounts
        V = delta_V_reset
        emit_spike()
    end

  end

  function set_V_th(v mV):
      Theta = v - E_L
  end

  function set_V_reset(v mV):
      delta_V_reset = v - E_L
  end

  function set_V_m(v mV):
      V = v - E_L
  end
end