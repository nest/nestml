/*
  A straight forward implementation of the RC circuit approach.
  Adds alpha shapes for synaptic currents which is modelled as an excplicit function.
*/
neuron rc_shape:

  state:
    V_m mV =  E_L
  end

  equations:
    shape g_ex = (e/tau_syn) * t * exp(-1/tau_syn*t)
    I_syn pA = curr_sum(g_ex, spikes) + currents + I_e
    V_m' = -(V_m - E_L)/tau_m  + I_syn/C_m
  end

  parameters:
    E_L   mV = -70mV
    C_m   pF = 250pF
    tau_m ms = 10ms
    tau_syn ms = 2.0ms

    alias V_th mV = -55mV - E_L
    alias V_reset mV = -70mV - E_L
    refractory_timeout ms = 2ms
    refractory_counts integer = 0
    I_e pA = 0pA
  end

  internals:
    PSConInit_E real = 1.0 * e / tau_syn
  end

  input:
    spikes   <- spike
    currents <- current
  end

  output: spike

  update:
    if refractory_counts == 0:
      integrate_odes()
      if V_m > V_th:
        V_m = V_reset
        emit_spike()
        refractory_counts = steps(refractory_timeout)
      end
    else:
      refractory_counts -= 1
    end

  end

end