"""
iaf_psc_exp_resolution_test
###########################

Description
+++++++++++

Used to test resolution() function.
"""
model iaf_psc_exp_resolution_test_neuron:
    state:
        I_syn_exc pA = 0 pA
        I_syn_inh pA = 0 pA
        V_m mV = E_L
        refr_t ms = 0 ms    # Refractory period timer
        is_refractory boolean = false
        a ms = resolution()

    equations:
        I_syn_exc' = -I_syn_exc / tau_syn_exc
        I_syn_inh' = -I_syn_inh / tau_syn_inh
        V_m' = -(V_m - E_L) / tau_m + (I_syn_exc - I_syn_inh + I_e + I_stim) / C_m

    parameters:
        C_m pF = 250 pF           # Capacitance of the membrane
        tau_m ms = 10 ms          # Membrane time constant
        tau_syn_inh ms = 2 ms     # Time constant of synaptic current
        tau_syn_exc ms = 2 ms     # Time constant of synaptic current
        refr_T ms = 2 ms          # Duration of refractory period
        E_L  mV = -70 mV          # Resting potential
        V_reset mV = -70 mV       # Reset value of the membrane potential
        Theta   mV = -55 mV       # Threshold potential

        # constant external input current
        I_e pA = 0 pA
        b ms = resolution()

    internals:
        c ms = resolution()

    input:
        spike_in_port <- spike
        I_stim pA <- continuous

    output:
        spike

    onReceive(spike_in_port):
        # route the incoming spike on the basis of the weight: less than zero means an inhibitory spike; greater than zero means an excitatory spike
        if spike_in_port < 0:
            I_syn_inh += spike_in_port * pA * s
        else:
            I_syn_exc += spike_in_port * pA * s

    update:
        d ms = resolution()
        if is_refractory:
            # neuron is absolute refractory, do not evolve ODEs
            refr_t -= resolution()
        else:
            # neuron not refractory, so evolve all ODEs (including V_m)
            integrate_odes()

    onCondition(not is_refractory and V_m >= Theta):
        # threshold crossing
        refr_t = refr_T    # start of the refractory period
        is_refractory = true
        V_m = V_reset
        emit_spike()

    onCondition(is_refractory and refr_t <= resolution() / 2):
        # end of refractory period
        refr_t = 0 ms
        is_refractory = false
