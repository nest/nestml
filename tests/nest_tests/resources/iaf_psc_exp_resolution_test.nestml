# iaf_psc_exp_resolution_test
# ###########################
#
# Description
# +++++++++++
#
# Used to test resolution() function.
#
# Copyright statement
# +++++++++++++++++++
#
# This file is part of NEST.
#
# Copyright (C) 2004 The NEST Initiative
#
# NEST is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 2 of the License, or
# (at your option) any later version.
#
# NEST is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with NEST.  If not, see <http://www.gnu.org/licenses/>.
#
model iaf_psc_exp_resolution_test_neuron:
    state:
        V_m mV = E_L
        refr_t ms = 0 ms    # Refractory period timer
        is_refractory boolean = false
        a ms = resolution()

    equations:
        kernel I_kernel_inh = exp(-t/tau_syn_inh)
        kernel I_kernel_exc = exp(-t/tau_syn_exc)
        inline I_syn pA = convolve(I_kernel_inh, inh_spikes) * pA + convolve(I_kernel_exc, exc_spikes) * pA + I_e + I_stim
        V_m' = -(V_m - E_L) / tau_m + I_syn / C_m

    parameters:
        C_m pF = 250 pF           # Capacitance of the membrane
        tau_m ms = 10 ms          # Membrane time constant
        tau_syn_inh ms = 2 ms     # Time constant of synaptic current
        tau_syn_exc ms = 2 ms     # Time constant of synaptic current
        refr_T ms = 2 ms          # Duration of refractory period
        E_L  mV = -70 mV          # Resting potential
        V_reset mV = -70 mV       # Reset value of the membrane potential
        Theta   mV = -55 mV       # Threshold potential

        # constant external input current
        I_e pA = 0 pA
        b ms = resolution()

    internals:
        c ms = resolution()

    input:
        exc_spikes <- excitatory spike
        inh_spikes <- inhibitory spike
        I_stim pA <- continuous

    output:
        spike

    update:
        d ms = resolution()
        if is_refractory:
            # neuron is absolute refractory, do not evolve ODEs
            refr_t -= resolution()
        else:
            # neuron not refractory, so evolve all ODEs (including V_m)
            integrate_odes()

    onCondition(not is_refractory and V_m >= Theta):
        # threshold crossing
        refr_t = refr_T    # start of the refractory period
        is_refractory = true
        V_m = V_reset
        emit_spike()

    onCondition(is_refractory and refr_t <= resolution() / 2):
        # end of refractory period
        refr_t = 0 ms
        is_refractory = false
