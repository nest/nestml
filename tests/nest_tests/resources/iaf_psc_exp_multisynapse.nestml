# iaf_psc_exp_multisynapse - Leaky integrate-and-fire neuron model with multiple ports
# ####################################################################################
#
# Description
# +++++++++++
#
# Used in NESTML unit testing.
#
# For more information about the model, see iaf_psc_exp in the ``models`` directory.
#
# For more information about "multisynapse" models, please refer to the NESTML documentation.
#
# Copyright statement
# +++++++++++++++++++
#
# This file is part of NEST.
#
# Copyright (C) 2004 The NEST Initiative
#
# NEST is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 2 of the License, or
# (at your option) any later version.
#
# NEST is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with NEST.  If not, see <http://www.gnu.org/licenses/>.
#
model iaf_psc_exp_multisynapse_neuron:
    state:
        V_m mV = E_L  # membrane potential
        refr_t ms = 0 ms    # Refractory period timer
        is_refractory boolean = false

    equations:
        kernel I_kernel1 = delta(t)
        kernel I_kernel2 = (e / tau_syn2) * t * exp(-t / tau_syn2)
        kernel I_kernel3 = -exp(-t / tau_syn3)

        recordable inline I_syn pA = unit_psc * (convolve(I_kernel1, spikes1) - convolve(I_kernel2, spikes2) + convolve(I_kernel3, spikes3))

        V_m' = -(V_m - E_L) / tau_m + (I_syn + I_e + I_stim) / C_m

    parameters:
        C_m pF = 250 pF           # Capacitance of the membrane
        tau_m ms = 10 ms          # Membrane time constant
        tau_syn2 ms = 2ms         # Time constant of synaptic current.
        tau_syn3 ms = 20ms        # Time constant of synaptic current.
        refr_T ms = 2 ms          # Duration of refractory period
        E_L mV = -70 mV           # Resting potential
        V_reset mV = -70 mV       # Reset value of the membrane potential
        V_th mV = -55 mV          # Spike threshold potential
        I_e pA = 0 pA             # External current.

        unit_psc pA = 1 pA        # Unitary postsynaptic current amplitude

    input:
        spikes1 <- spike
        spikes2 <- spike
        spikes3 <- spike
        I_stim pA <- continuous

    output:
        spike

    update:
        if is_refractory:
            # neuron is absolute refractory, do not evolve ODEs
            refr_t -= resolution()
        else:
            # neuron not refractory, so evolve all ODEs (including V_m)
            integrate_odes()

    onCondition(not is_refractory and V_m >= V_th):
        # threshold crossing
        refr_t = refr_T    # start of the refractory period
        is_refractory = true
        V_m = V_reset
        emit_spike()

    onCondition(is_refractory and refr_t <= resolution() / 2):
        # end of refractory period
        refr_t = 0 ms
        is_refractory = false
