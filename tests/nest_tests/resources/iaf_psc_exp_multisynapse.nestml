/*
Name: iaf_psc_exp_multisynapse - Leaky integrate-and-fire neuron model with
                                 multiple ports.

Description:

 iaf_psc_exp_multisynapse is a direct extension of iaf_psc_exp.
 On the postsynapic side, there can be arbitrarily many synaptic
 time constants (iaf_psc_exp has exactly two: tau_syn_ex and tau_syn_in).

 This can be reached by specifying separate receptor ports, each for
 a different time constant. The port number has to match the respective
 "receptor_type" in the connectors.

Sends: SpikeEvent

Receives: SpikeEvent, CurrentEvent, DataLoggingRequest

Author:  Plesser, adapted from iaf_psc_alpha_multisynapse
SeeAlso: iaf_psc_alpha, iaf_psc_delta, iaf_psc_exp, iaf_cond_exp,
iaf_psc_alpha_multisynapse
*/
neuron iaf_psc_exp_multisynapse_neuron:
  state:
    r integer                  # counts number of tick during the refractory period
  end

  initial_values:
    V_abs mV = 0 mV # membrane potential
  end

  equations:
      shape I_shape1 = exp(-1/tau_syn1*t)
      shape I_shape2 = exp(-1/tau_syn2*t)
      shape I_shape3 = -exp(-1/tau_syn3*t)
      inline I_syn pA = (convolve(I_shape1, spikes1) - convolve(I_shape2, spikes2) + convolve(I_shape3, spikes3) + I_e + currents)
      recordable inline V_m mV = V_abs + E_L # Relative membrane potential.
                                  # I.e. the real threshold is (V_m-E_L).
      V_abs' = -V_abs / tau_m + I_syn / C_m
  end

  parameters:
    C_m      pF = 250pF  # Capacity of the membrane
    tau_m    ms = 10ms # Membrane time constant.
    tau_syn1 ms = .2ms     # Time constant of synaptic current.
    tau_syn2 ms = 2ms     # Time constant of synaptic current.
    tau_syn3 ms = 20ms     # Time constant of synaptic current.
    t_ref    ms = 2ms    # Refractory period.
    E_L      mV = -70mV    # Resting potential.
    V_reset mV = -70.0 mV - E_L # Reset value of the membrane potential
                                     # RELATIVE TO RESTING POTENTIAL(!).
                                     # I.e. the real threshold is (V_reset + E_L).
    V_th   mV = -55.0 mV - E_L # Threshold, RELATIVE TO RESTING POTENTIAL(!).
                                    # I.e. the real threshold is (E_L+V_th).
    I_e     pA = 0pA     # External current.
  end

  internals:
    # refractory time in steps
    RefractoryCounts integer = steps(t_ref)
  end

  input:
    spikes1 nS <- spike
    spikes2 nS <- spike
    spikes3 nS <- spike
    currents <- current
  end

  output: spike

  update:
    if r == 0: # neuron not refractory, so evolve V_abs
      integrate_odes()
    else:
      r = r - 1 # neuron is absolute refractory
    end

    if V_abs >= V_th: # threshold crossing
      r = RefractoryCounts
      V_abs = V_reset
      emit_spike()
    end

  end

end
