"""
FIR_filter.nestml
################


Description
+++++++++++

Model for Finite Impulse Response (FIR) filter


Copyright statement
+++++++++++++++++++

This file is part of NEST.

Copyright (C) 2004 The NEST Initiative

NEST is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 2 of the License, or
(at your option) any later version.

NEST is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with NEST.  If not, see <http://www.gnu.org/licenses/>.
"""
model fir_filter:
    state:
        spike_in_buffer real = 0.

        # FIR filter output (to be recorded by NEST multimeter)
        y real = 0.

        # circular buffer for input spike count per timestep
        x[N] real = 0.
        i integer = 0

    parameters:
        N integer = 1  # filter order
        h[N] real = 1.   # filter coefficients

    input:
        spike_in <- spike(weight real)


    onReceive(spike_in):
        spike_in_buffer += spike_in.weight

    update:
        # circular buffer for input spike count per timestep
        x[i] = spike_in_buffer * s
        spike_in_buffer = 0.

        # compute the new value of y
        j integer = 0
        k integer = 0
        y = 0
        for j in 0 ... N step 1:
            k = (i - j + N) % N   # shift the index due to circular buffer starting position
            y += x[k] * h[j]

        # Increment i to receive the next input spike
        i += 1
        i = i % N
