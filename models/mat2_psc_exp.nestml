/*
   Name: mat2_psc_exp_nestml - Non-resetting leaky integrate-and-fire neuron model with
   exponential PSCs and adaptive threshold.

   Description:
   mat2_psc_exp_nestml is an implementation of a leaky integrate-and-fire model
   with exponential shaped postsynaptic currents (PSCs). Thus, postsynaptic
   currents have an infinitely short rise time.

   The threshold is lifted when the neuron is fired and then decreases in a
   fixed time scale toward a fixed level [3].

   The threshold crossing is followed by a total refractory period
   during which the neuron is not allowed to fire, even if the membrane
   potential exceeds the threshold. The membrane potential is NOT reset,
   but continuously integrated.

   The linear subthresold dynamics is integrated by the Exact
   Integration scheme [1]. The neuron dynamics is solved on the time
   grid given by the computation step size. Incoming as well as emitted
   spikes are forced to that grid.

   An additional state variable and the corresponding differential
   equation represents a piecewise constant external current.

   The general framework for the consistent formulation of systems with
   neuron like dynamics interacting by point events is described in
   [1]. A flow chart can be found in [2].

   Remarks:
   The present implementation uses individual variables for the
   components of the state vector and the non-zero matrix elements of
   the propagator. Because the propagator is a lower triangular matrix
   no full matrix multiplication needs to be carried out and the
   computation can be done "in place" i.e. no temporary state vector
   object is required.

   Remarks:
   tau_m != tau_syn_{ex,in} is required by the current implementation to avoid a
   degenerate case of the ODE describing the model [1]. For very similar values,
   numerics will be unstable.

   References:
   [1] Rotter S & Diesmann M (1999) Exact simulation of
       time-invariant linear systems with applications to neuronal
       modeling. Biologial Cybernetics 81:381-402.
   [2] Diesmann M, Gewaltig M-O, Rotter S, & Aertsen A (2001) State
       space analysis of synchronous spiking in cortical neural
       networks. Neurocomputing 38-40:565-571.
   [3] Kobayashi R, Tsubo Y and Shinomoto S (2009) Made-to-order
       spiking neuron model equipped with a multi-timescale adaptive
       threshold. Front. Comput. Neurosci. 3:9. doi:10.3389/neuro.10.009.2009

   Sends: SpikeEvent

   Receives: SpikeEvent, CurrentEvent, DataLoggingRequest

   FirstVersion: Mai 2009
   Author: Thomas Pfeil (modified iaf_psc_exp model of Moritz Helias)
*/
neuron mat2_psc_exp_neuron:

  state:
    V_abs mV  # Membrane potential
    V_th_alpha_1 mV # Two-timescale adaptive threshold
    V_th_alpha_2 mV # Two-timescale adaptive threshold
    alias V_m mV = V_abs + E_L    # Relative membrane potential.
                                  # I.e. the real threshold is (V_m-E_L).
  end

  equations:
    shape I_shape_in = exp(-1/tau_syn_in*t)
    shape I_shape_ex = exp(-1/tau_syn_ex*t)

    # V_th_alpha_1' = -V_th_alpha_1/tau_1
    # V_th_alpha_2' = -V_th_alpha_2/tau_2
    I_syn pA = (curr_sum(I_shape_in, in_spikes) + curr_sum(I_shape_ex, ex_spikes) + I_e + currents)
    V_abs' = -1/tau_m * V_abs + 1/C_m * I_syn
  end

  parameters:
    tau_m        ms =     5ms  # Membrane time constant in ms
    C_m          pF =   100pF  # Capacity of the membrane in pF
    t_ref        ms =     2ms  # Duration of absolute refractory period (no spiking) in ms
    E_L          mV = -70.0mV  # Resting potential in mV
    I_e          pA =     0pA  # Constant input current in pA
    tau_syn_ex   ms =     1ms  # Time constant of postsynaptic excitatory currents in ms
    tau_syn_in   ms =     3ms  # Time constant of postsynaptic inhibitory currents in ms
    tau_1        ms =    10ms  # Short time constant of adaptive threshold in ms
    tau_2        ms =   200ms  # Long time constant of adaptive threshold in ms
    alpha_1      mV =  37.0mV  # Amplitude of short time threshold adaption in mV [3]
    alpha_2      mV =   2.0mV  # Amplitude of long time threshold adaption in mV [3]
    omega        mV =  19.0mV  # Resting spike threshold in mV (absolute value, not relative to E_L)
  end

  internals:
    h ms = resolution()
    P11th real = exp( -h / tau_1 )
    P22th real = exp( -h / tau_2 )

    RefractoryCounts integer = steps(t_ref) # refractory time in steps
    r integer                               # counts number of tick during the refractory period
  end

  input:
    ex_spikes   <- excitatory spike
    in_spikes   <- inhibitory spike
    currents    <- current
  end

  output: spike

  update:
    # evolve membrane potential
    integrate_odes()

    # evolve adaptive threshold
    V_th_alpha_1 *= P11th
    V_th_alpha_2 *= P22th

    if r == 0: # not refractory
      if V_abs >= omega + V_th_alpha_1 + V_th_alpha_2: # threshold crossing
          r = RefractoryCounts

          # procedure for adaptive potential
          V_th_alpha_1 += alpha_1 # short time
          V_th_alpha_2 += alpha_2 # long time

          emit_spike()
      end
    else:
        r = r - 1
    end

  end

end
