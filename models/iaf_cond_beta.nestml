/* Name: iaf_cond_beta - Simple conductance based leaky integrate-and-fire neuron model.

Description:
iaf_cond_beta is an implementation of a spiking neuron using IAF dynamics with
conductance-based synapses. Incoming spike events induce a post-synaptic change
of conductance modelled by an beta function. The beta function
is normalised such that an event of weight 1.0 results in a peak current of 1 nS
at t = tau_syn.

References:
Meffin, H., Burkitt, A. N., & Grayden, D. B. (2004). An analytical
model for the large, fluctuating synaptic conductance state typical of
neocortical neurons in vivo				. J.  Comput. Neurosci., 16, 159-175.
Bernander, O ., Douglas, R. J., Martin, K. A. C., & Koch, C. (1991).
Synaptic background activity influences spatiotemporal integration in
single pyramidal cells.  Proc. Natl. Acad. Sci. USA, 88(24),
11569-11573.
Kuhn, Aertsen, Rotter (2004) Neuronal Integration of Synaptic Input in
the Fluctuation- Driven Regime. Jneurosci 24(10) 2345-2356
Author: Schrader, Plesser
SeeAlso: iaf_cond_exp, iaf_cond_beta_mc, iaf_cond_alpha */

neuron iaf_cond_beta_neuron:
  state:
    r integer             # counts number of tick during the refractory period
  end

  initial_values:
    V_m mV = E_L          # membrane potential
    g_in nS = 0 nS        # inputs from the inh conductance
    g_in' nS/ms = 0 nS/ms # inputs from the inh conductance
    g_ex nS = 0 nS        # inputs from the exc conductance
    g_ex' nS/ms = 0 nS/ms # inputs from the exc conductance
  end

  equations:
      g_in'' = -g_in'/tau_syn_rise_I
      g_in' = g_in' - g_in/tau_syn_decay_I

      g_ex'' = -g_ex'/tau_syn_rise_E
      g_ex' = g_ex' -g_ex/tau_syn_decay_E

      function I_syn_exc pA = (F_E + convolve(g_ex, spikeExc)) * ( V_m - E_ex )
      function I_syn_inh pA = (F_I + convolve(g_in, spikeInh)) * ( V_m - E_in )
      function I_leak pA = g_L * ( V_m - E_L ) # pa = nS * mV
      V_m' =  (-I_leak - I_syn_exc - I_syn_inh + currents + I_e ) / C_m
  end

  parameters:
    E_L mV = -85.0mV # Leak reversal Potential (aka resting potential) in mV
    C_m pF = 250.0pF # Capacity of the membrane
    t_ref ms = 2.0ms # Refractory period in ms
    V_th mV = -55.0mV      # Threshold Potential in mV
    V_reset mV = -60.0mV   # Reset Potential in mV
    E_ex mV = 0mV          # Excitatory reversal Potential in mV
    E_in mV = -85.0mV      # Inhibitory reversal Potential in mV
    g_L nS = 16.6667nS     # Leak Conductance in nS
    tau_syn_rise_I ms = 0.2ms    # Synaptic Time Constant Excitatory Synapse in ms
    tau_syn_decay_I ms = 2.0ms   # Synaptic Time Constant for Inhibitory Synapse in ms
    tau_syn_rise_E ms = 0.2ms    # Synaptic Time Constant Excitatory Synapse in ms
    tau_syn_decay_E ms = 2.0ms   # Synaptic Time Constant for Inhibitory Synapse in ms
    I_e pA = 0pA            # Constant Current in pA
    F_E nS = 0nS            # Constant External input conductance in nS (excitatory).
    F_I nS = 0nS            # Constant External input conductance in nS (inhibitory).
  end

  internals:

    # conductance excursion.
    PSConInit_E 1/ms = e / tau_syn_rise_E

    # Impulse to add to g_in' on spike arrival to evoke unit-amplitude
    # conductance excursion.
    PSConInit_I 1/ms = e / tau_syn_rise_I

    RefractoryCounts integer = steps(t_ref) # refractory time in steps
  end

  input:
      spikeInh nS  <- inhibitory spike
      spikeExc nS  <- excitatory spike
      currents <- current
  end

  output: spike

  update:

    integrate_odes()
    if r != 0: # not refractory
      r =  r - 1
      V_m = V_reset # clamp potential

    elif V_m >= V_th:
      r = RefractoryCounts
      V_m = V_reset # clamp potential
      emit_spike()

    end

    g_ex' += spikeExc * PSConInit_E
    g_in' += spikeInh * PSConInit_I
  end

end
