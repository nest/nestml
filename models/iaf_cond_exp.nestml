"""
Name: iaf_cond_exp - Simple conductance based leaky integrate-and-fire neuron
                     model.

Description:
iaf_cond_exp is an implementation of a spiking neuron using IAF dynamics with
conductance-based synapses. Incoming spike events induce a post-synaptic change
of conductance modelled by an exponential function. The exponential function
is normalised such that an event of weight 1.0 results in a peak conductance of
1 nS.

Sends: SpikeEvent

Receives: SpikeEvent, CurrentEvent, DataLoggingRequest

References:

Meffin, H., Burkitt, A. N., & Grayden, D. B. (2004). An analytical
model for the large, fluctuating synaptic conductance state typical of
neocortical neurons in vivo. J.  Comput. Neurosci., 16, 159-175.

Author: Sven Schrader

SeeAlso: iaf_psc_delta, iaf_psc_exp, iaf_cond_exp
"""
neuron iaf_cond_exp:

  state:
    r integer        # counts number of tick during the refractory period
  end

  initial_values:
    V_m mV = E_L     # membrane potential
  end

  equations:
    shape g_in = exp(-t/tau_syn_in) # inputs from the inh conductance
    shape g_ex = exp(-t/tau_syn_ex) # inputs from the exc conductance

    inline I_syn_exc pA = convolve(g_ex, spikeExc) * ( V_m - E_ex )
    inline I_syn_inh pA = convolve(g_in, spikeInh) * ( V_m - E_in )
    inline I_leak pA = g_L * ( V_m - E_L )
    V_m' = ( -I_leak - I_syn_exc - I_syn_inh + currents + I_e ) / C_m
  end

  parameters:
    V_th mV = -55.0 mV     # Threshold Potential
    V_reset mV = -60.0 mV  # Reset Potential
    t_ref ms = 2.0 ms     # Refractory period
    g_L nS = 16.6667 nS    # Leak Conductance
    C_m pF = 250.0 pF     # Membrane Capacitance
    E_ex mV = 0 mV         # Excitatory reversal Potential
    E_in mV = -85.0 mV     # Inhibitory reversal Potential
    E_L mV = -70.0 mV      # Leak reversal Potential (aka resting potential)
    tau_syn_ex ms = 0.2 ms # Synaptic Time Constant Excitatory Synapse
    tau_syn_in ms = 2.0 ms # Synaptic Time Constant for Inhibitory Synapse
    I_e pA = 0 pA          # Constant Current
  end

  internals:
    RefractoryCounts integer = steps(t_ref) # refractory time in steps
  end

  input:
      spikeInh nS <- inhibitory spike
      spikeExc nS <- excitatory spike
      currents <- current
  end

  output: spike

  update:
    integrate_odes()
    if r != 0: # neuron is absolute refractory
      r =  r - 1
      V_m = V_reset # clamp potential
    elif V_m >= V_th:  # neuron is not absolute refractory
      r = RefractoryCounts
      V_m = V_reset # clamp potential
      emit_spike()
    end

  end

end

"""
Name: iaf_cond_exp_implicit - Simple conductance based leaky integrate-and-fire neuron
                              model.

Description:
iaf_cond_exp_implicit is an implementation of a spiking neuron using IAF dynamics with
conductance-based synapses. Incoming spike events induce a post-synaptic change
of conductance modelled by an exponential function. The exponential function
is normalised such that an event of weight 1.0 results in a peak conductance of
1 nS.

Sends: SpikeEvent

Receives: SpikeEvent, CurrentEvent, DataLoggingRequest

References:

Meffin, H., Burkitt, A. N., & Grayden, D. B. (2004). An analytical
model for the large, fluctuating synaptic conductance state typical of
neocortical neurons in vivo. J.  Comput. Neurosci., 16, 159-175.

Author: Sven Schrader

SeeAlso: iaf_psc_delta, iaf_psc_exp, iaf_cond_exp
"""
neuron iaf_cond_exp_implicit:

  state:
    r integer  # counts number of tick during the refractory period
  end

  initial_values:
    g_in nS = 1 nS  # inputs from the inh conductance
    g_ex nS = 1 nS  # inputs from the exc conductance
    V_m mV = E_L   # membrane potential
  end

  equations:
    shape g_in' = -g_in/tau_synI
    shape g_ex' = -g_ex/tau_synE
    inline I_syn_exc pA = convolve(g_ex, spikeExc) * ( V_m - E_ex )
    inline I_syn_inh pA = convolve(g_in, spikeInh) * ( V_m - E_in )
    inline I_leak pA = g_L * ( V_m - E_L )
    V_m' = ( -I_leak - I_syn_exc - I_syn_inh + currents + I_e ) / C_m
  end

  parameters:
    V_th mV = -55.0 mV    # Threshold Potential
    V_reset mV = -60.0 mV # Reset Potential
    t_ref ms = 2.0 ms    # Refractory period
    g_L nS = 16.6667 nS   # Leak Conductance
    C_m pF = 250.0 pF    # Membrane Capacitance
    E_ex mV = 0 mV        # Excitatory reversal Potential
    E_in mV = -85.0 mV    # Inhibitory reversal Potential
    E_L mV = -70.0 mV     # Leak reversal Potential (aka resting potential)
    tau_synE ms = 0.2 ms  # Synaptic Time Constant Excitatory Synapse
    tau_synI ms = 2.0 ms  # Synaptic Time Constant for Inhibitory Synapse
    I_e pA = 0 pA         # Constant Current
  end

  internals:
    RefractoryCounts integer = steps(t_ref) # refractory time in steps
  end

  input:
      spikeInh nS <- inhibitory spike
      spikeExc nS <- excitatory spike
      currents <- current
  end

  output: spike

  update:
    integrate_odes()
    if r != 0: # neuron is absolute refractory
      r =  r - 1
      V_m = V_reset # clamp potential
    elif V_m >= V_th:  # neuron is not absolute refractory
      r = RefractoryCounts
      V_m = V_reset # clamp potential
      emit_spike()
    end

  end

end
