/*
Name: iaf_cond_exp_neuron - Simple conductance based leaky integrate-and-fire neuron
                              model.

Description:
iaf_cond_exp is an implementation of a spiking neuron using IAF dynamics with
conductance-based synapses. Incoming spike events induce a post-synaptic change
of conductance modelled by an exponential function. The exponential function
is normalised such that an event of weight 1.0 results in a peak conductance of
1 nS.

Sends: SpikeEvent

Receives: SpikeEvent, CurrentEvent, DataLoggingRequest

References:

Meffin, H., Burkitt, A. N., & Grayden, D. B. (2004). An analytical
model for the large, fluctuating synaptic conductance state typical of
neocortical neurons in vivo. J.  Comput. Neurosci., 16, 159-175.

Author: Sven Schrader

SeeAlso: iaf_psc_delta, iaf_psc_exp, iaf_cond_exp
*/
neuron iaf_cond_exp_neuron:

  state:
    V_m mV = E_L     ## membrane potential
    r integer        ## counts number of tick during the refractory period
  end

  equations:
    shape g_in = exp(-t/tau_syn_in) ## inputs from the inh conductance
    shape g_ex = exp(-t/tau_syn_ex) ## inputs from the exc conductance

    function I_syn_exc pA = convolve(g_ex, spikeExc)  * ( V_m - E_ex )
    function I_syn_inh pA = convolve(g_in, spikeInh)  * ( V_m - E_in )
    function I_leak pA = g_L * ( V_m - E_L )
    V_m' = ( -I_leak - I_syn_exc - I_syn_inh + currents + I_e ) / C_m
  end

  parameters:
    V_th mV = -55.0mV     ## Threshold Potential in mV
    V_reset mV = -60.0mV  ## Reset Potential in mV
    t_ref ms = 2.0 ms     ## Refractory period in ms
    g_L nS = 16.6667nS    ## Leak Conductance in nS
    C_m pF = 250.0 pF     ## Membrane Capacitance in pF
    E_ex mV = 0mV         ## Excitatory reversal Potential in mV
    E_in mV = -85.0mV     ## Inhibitory reversal Potential in mV
    E_L mV = -70.0mV      ## Leak reversal Potential (aka resting potential) in mV
    tau_syn_ex ms = 0.2ms ## Synaptic Time Constant Excitatory Synapse in ms
    tau_syn_in ms = 2.0ms ## Synaptic Time Constant for Inhibitory Synapse in ms
    I_e pA = 0pA          ## Constant Current in pA
  end

  internals:
    RefractoryCounts integer = steps(t_ref) # refractory time in steps
  end

  input:
      spikeInh nS <- inhibitory spike
      spikeExc nS <- excitatory spike
      currents <- current
  end

  output: spike

  update:
    integrate_odes()
    if r != 0: # neuron is absolute refractory
      r =  r - 1
      V_m = V_reset # clamp potential
    elif V_m >= V_th:  # neuron is not absolute refractory
      r = RefractoryCounts
      V_m = V_reset # clamp potential
      emit_spike()
    end

  end

end

/*
Name: iaf_cond_exp_implicit - Simple conductance based leaky integrate-and-fire neuron
                              model.

Description:
iaf_cond_exp is an implementation of a spiking neuron using IAF dynamics with
conductance-based synapses. Incoming spike events induce a post-synaptic change
of conductance modelled by an exponential function. The exponential function
is normalised such that an event of weight 1.0 results in a peak conductance of
1 nS.

Sends: SpikeEvent

Receives: SpikeEvent, CurrentEvent, DataLoggingRequest

References:

Meffin, H., Burkitt, A. N., & Grayden, D. B. (2004). An analytical
model for the large, fluctuating synaptic conductance state typical of
neocortical neurons in vivo. J.  Comput. Neurosci., 16, 159-175.

Author: Sven Schrader

SeeAlso: iaf_psc_delta, iaf_psc_exp, iaf_cond_exp
*/
neuron iaf_cond_exp_implicit:

  state:
    g_in nS = 0nS  ## inputs from the inh conductance
    g_ex nS = 0nS  ## inputs from the exc conductance
    V_m nS = E_L   ## membrane potential
    r integer  ## counts number of tick during the refractory period
  end

  equations:
    g_in' = -g_in/tau_synI
    g_ex' = -g_ex/tau_synE
    function I_syn_exc pA = convolve(g_ex, spikeExc)  * ( V_m - E_ex )
    function I_syn_inh pA = convolve(g_in, spikeInh)  * ( V_m - E_in )
    function I_leak pA = g_L * ( V_m - E_L )
    V_m' = ( -I_leak - I_syn_exc - I_syn_inh + currents + I_e ) / C_m
  end

  parameters:
    V_th mV = -55.0mV    ## Threshold Potential in mV
    V_reset mV = -60.0mV ## Reset Potential in mV
    t_ref ms = 2.0 ms    ## Refractory period in ms
    g_L nS = 16.6667nS   ## Leak Conductance in nS
    C_m pF = 250.0 pF    ## Membrane Capacitance in pF
    E_ex mV = 0mV        ## Excitatory reversal Potential in mV
    E_in mV = -85.0mV    ## Inhibitory reversal Potential in mV
    E_L mV = -70.0mV     ## Leak reversal Potential (aka resting potential) in mV
    tau_synE ms = 0.2ms  ## Synaptic Time Constant Excitatory Synapse in ms
    tau_synI ms = 2.0ms  ## Synaptic Time Constant for Inhibitory Synapse in ms
    I_e pA = 0pA         ## Constant Current in pA
  end

  internals:
    RefractoryCounts integer = steps(t_ref) ## refractory time in steps
  end

  input:
      spikeInh nS <- inhibitory spike
      spikeExc nS <- excitatory spike
      currents <- current
  end

  output: spike

  update:
    integrate_odes()
    if r != 0: # neuron is absolute refractory
      r =  r - 1
      V_m = V_reset # clamp potential
    elif V_m >= V_th:  # neuron is not absolute refractory
      r = RefractoryCounts
      V_m = V_reset # clamp potential
      emit_spike()
    end

    # add incoming spikes
    g_ex += spikeExc
    g_in += spikeInh
  end

end
