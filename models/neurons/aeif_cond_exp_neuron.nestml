# aeif_cond_exp - Conductance based exponential integrate-and-fire neuron model
# #############################################################################
# 
# Description
# +++++++++++
# 
# aeif_cond_exp is the adaptive exponential integrate and fire neuron
# according to Brette and Gerstner (2005), with post-synaptic
# conductances in the form of truncated exponentials.
# 
# The membrane potential is given by the following differential equation:
# 
# .. math::
# 
#    C_m \frac{dV_m}{dt} =
#    -g_L(V_m-E_L)+g_L\Delta_T\exp\left(\frac{V_m-V_{th}}{\Delta_T}\right) - g_e(t)(V_m-E_e) \\
#    -g_i(t)(V_m-E_i)-w +I_e
# 
# and
# 
# .. math::
# 
#    \tau_w \frac{dw}{dt} = a(V_m-E_L) - w
# 
# Note that the membrane potential can diverge to positive infinity due to the exponential term. To avoid numerical instabilities, instead of :math:`V_m`, the value :math:`\min(V_m,V_{peak})` is used in the dynamical equations.
# 
# .. note::
# 
#    The default refractory period for ``aeif`` models is zero, consistent with the model definition in
#    Brette & Gerstner [1]_.  Thus, an ``aeif`` neuron with default parameters can fire multiple spikes in a single
#    time step, which can lead to exploding spike numbers and extreme slow-down of simulations.
#    To avoid such unphysiological behavior, you should set a refractory time ``refr_t > 0``.
# 
# 
# References
# ++++++++++
# 
# .. [1] Brette R and Gerstner W (2005). Adaptive exponential
#        integrate-and-fire model as an effective description of neuronal
#        activity. Journal of Neurophysiology. 943637-3642
#        DOI: https://doi.org/10.1152/jn.00686.2005
# 
# 
# See also
# ++++++++
# 
# iaf_cond_exp, aeif_cond_alpha
#
#
# Copyright statement
# +++++++++++++++++++
#
# This file is part of NEST.
# 
# Copyright (C) 2004 The NEST Initiative
#
# NEST is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 2 of the License, or
# (at your option) any later version.
#
# NEST is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with NEST.  If not, see <http://www.gnu.org/licenses/>.
#
#
model aeif_cond_exp_neuron:

    state:
        V_m mV = E_L   # Membrane potential
        w pA = 0 pA    # Spike-adaptation current
        refr_t ms = 0 ms    # Refractory period timer

    equations:
        inline V_bounded mV = min(V_m, V_peak)    # prevent exponential divergence
        kernel g_inh = exp(-t / tau_syn_inh)
        kernel g_exc = exp(-t / tau_syn_exc)

        # Add inlines to simplify the equation definition of V_m
        inline exp_arg real = (V_bounded - V_th) / Delta_T
        inline I_spike pA = g_L * Delta_T * exp(exp_arg)
        inline I_syn_exc pA = convolve(g_exc, exc_spikes) * nS * (V_bounded - E_exc)
        inline I_syn_inh pA = convolve(g_inh, inh_spikes) * nS * (V_bounded - E_inh)

        V_m' = (-g_L * (V_bounded - E_L) + I_spike - I_syn_exc - I_syn_inh - w + I_e + I_stim) / C_m
        w' = (a * (V_bounded - E_L) - w) / tau_w

        refr_t' = -1e3 * ms/s    # refractoriness is implemented as an ODE, representing a timer counting back down to zero. XXX: TODO: This should simply read ``refr_t' = -1 / s`` (see https://github.com/nest/nestml/issues/984)

    parameters:
        # membrane parameters
        C_m pF = 281.0 pF       # Membrane Capacitance
        refr_T ms = 2 ms        # Duration of refractory period
        V_reset mV = -60.0 mV   # Reset Potential
        g_L nS = 30.0 nS        # Leak Conductance
        E_L mV = -70.6 mV       # Leak reversal Potential (aka resting potential)

        # spike adaptation parameters
        a nS = 4 nS             # Subthreshold adaptation
        b pA = 80.5 pA          # Spike-triggered adaptation
        Delta_T mV = 2.0 mV     # Slope factor
        tau_w ms = 144.0 ms     # Adaptation time constant
        V_th mV = -50.4 mV      # Spike initiation threshold
        V_peak mV = 0 mV        # Spike detection threshold

        # synaptic parameters
        E_exc mV = 0 mV            # Excitatory reversal Potential
        tau_syn_exc ms = 0.2 ms    # Synaptic Time Constant Excitatory Synapse
        E_inh mV = -85.0 mV        # Inhibitory reversal Potential
        tau_syn_inh ms = 2.0 ms    # Synaptic Time Constant for Inhibitory Synapse

        # constant external input current
        I_e pA = 0 pA

    input:
        inh_spikes <- inhibitory spike
        exc_spikes <- excitatory spike
        I_stim pA <- continuous

    output:
        spike

    update:
        if refr_t > 0 ms:
            # neuron is absolute refractory, do not evolve V_m
            integrate_odes(w, refr_t)
        else:
            # neuron not refractory
            integrate_odes(w, V_m)

    onCondition(refr_t <= 0 ms and V_m >= V_peak):
        # threshold crossing
        refr_t = refr_T    # start of the refractory period
        V_m = V_reset # clamp potential
        w += b
        emit_spike()
