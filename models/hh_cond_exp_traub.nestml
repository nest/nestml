/*
Name: hh_cond_exp_traub - Hodgin Huxley based model, Traub modified.

Description:

 hh_cond_exp_traub is an implementation of a modified Hodkin-Huxley model

 (1) Post-synaptic currents
 Incoming spike events induce a post-synaptic change of conductance modeled
 by an exponential function. The exponential function is normalized such that an
 event of weight 1.0 results in a peak current of 1 nS.

 (2) Spike Detection
 Spike detection is done by a combined threshold-and-local-maximum search: if
 there is a local maximum above a certain threshold of the membrane potential,
 it is considered a spike.

Problems/Todo:
Only the channel variables m,h,n are implemented. The original
contains variables called y,s,r,q and \chi.
References:

Traub, R.D. and Miles, R. (1991) Neuronal Networks of the Hippocampus.
Cambridge University Press, Cambridge UK.

Sends: SpikeEvent

Receives: SpikeEvent, CurrentEvent, DataLoggingRequest

Author: Schrader

SeeAlso: hh_psc_alpha
*/
neuron hh_cond_exp_traub_neuron:

  state:
    V_m mV = E_L ##  Membrane potential

    # equilibrium values for (in)activation variables
    function alpha_n_init 1/ms = 0.032/(ms* mV ) * ( 15. mV - V_m) / ( exp( ( 15. mV - V_m) / 5. mV ) - 1. )
    function beta_n_init 1/ms = 0.5 / ms * exp( ( 10. mV - V_m ) / 40. mV )
    function alpha_m_init 1/ms = 0.32/(ms* mV ) * ( 13. mV - V_m) / ( exp( ( 13. mV - V_m) / 4. mV ) - 1. )
    function beta_m_init 1/ms = 0.28/(ms* mV ) * ( V_m  - 40. mV ) / ( exp( ( V_m - 40. mV ) / 5. mV ) - 1. )
    function alpha_h_init 1/ms = 0.128/ms * exp( ( 17. mV - V_m) / 18. mV )
    function beta_h_init 1/ms = ( 4. / ( 1. + exp( ( 40. mV - V_m ) / 5. mV) ) ) / ms

    Act_m real =  alpha_m_init / ( alpha_m_init + beta_m_init )
    Act_h real = alpha_h_init / ( alpha_h_init + beta_h_init )
    Inact_n real =  alpha_n_init / ( alpha_n_init + beta_n_init )
  end

  equations:
    shape g_in = exp(-1/tau_syn_in*t)
    shape g_ex = exp(-1/tau_syn_ex*t)

    # Add aliases to simplify the equation definition of V_m
    # ionic currents
    function I_Na  pA = g_Na * Act_m * Act_m * Act_m * Act_h * ( V_m - E_Na )
    function I_K   pA  = g_K * Inact_n * Inact_n * Inact_n * Inact_n * ( V_m - E_K )
    function I_L   pA = g_L * ( V_m - E_L )

    function I_syn_exc pA = cond_sum(g_ex, spikeExc) * ( V_m - E_ex )
    function I_syn_inh pA = cond_sum(g_in, spikeInh) * ( V_m - E_in )

    # membrane potential
    V_m' =( -I_Na - I_K - I_L - I_syn_exc - I_syn_inh + I_stim + I_e ) / C_m

    # channel dynamics
    function V_rel mV = V_m - V_T
    function alpha_n 1/ms = 0.032/(ms* mV ) * ( 15. mV - V_rel) / ( exp( ( 15. mV - V_rel) / 5. mV ) - 1. )
    function beta_n 1/ms = 0.5 /ms * exp( ( 10. mV - V_rel ) / 40. mV )
    function alpha_m 1/ms = 0.32/(ms* mV ) * ( 13. mV - V_rel) / ( exp( ( 13. mV - V_rel) / 4. mV ) - 1. )
    function beta_m 1/ms = 0.28/(ms* mV ) * ( V_rel  - 40. mV ) / ( exp( ( V_rel - 40. mV ) / 5. mV ) - 1. )
    function alpha_h 1/ms = 0.128/ms * exp( ( 17. mV - V_rel) / 18. mV )
    function beta_h 1/ms = ( 4. / ( 1. + exp( ( 40. mV - V_rel ) / 5. mV) ) ) / ms

    Act_m' = ( alpha_m - ( alpha_m + beta_m ) * Act_m ) 
    Act_h' = ( alpha_h - ( alpha_h + beta_h ) * Act_h ) 
    Inact_n' = ( alpha_n - ( alpha_n + beta_n ) * Inact_n )

  end

  parameters:
    g_Na nS = 20000.0nS       # Threshold Potential in mV
    g_K nS = 6000.0nS         # K Conductance
    g_L nS = 10nS             # Leak Conductance
    C_m pF = 200.0pF          # Membrane Capacitance in pF
    E_Na mV = 50mV            # Reversal potentials
    E_K mV = -90.mV           # Potassium reversal potential
    E_L mV = -60.mV           # Leak reversal Potential (aka resting potential) in mV
    V_T mV = -63.0mV          # Voltage offset that controls dynamics. For default
                              # parameters, V_T = -63mV results in a threshold around -50mV.
    tau_syn_ex ms = 5.0ms     # Synaptic Time Constant Excitatory Synapse in ms
    tau_syn_in ms = 10.0ms    # Synaptic Time Constant for Inhibitory Synapse in ms
    I_e pA = 0pA              # Constant Current in pA
    E_ex mV = 0.0 mV          # Excitatory synaptic reversal potential
    E_in mV = -80.0mV         # Inhibitory synaptic reversal potential
    I_stim pA = 0pA           # External input current
  end

  internals:
    RefractoryCounts integer = 20
    r integer # counts number of tick during the refractory period
  end

  input:
      spikeInh   <- inhibitory spike
      spikeExc   <- excitatory spike
      currents <- current
  end

  output: spike

  update:
    U_old mV = V_m
    integrate_odes()

    # sending spikes: crossing 0 mV, pseudo-refractoriness and local maximum...
    if r > 0:
      r -= 1
    elif V_m > V_T + 30mV and U_old > V_m:
      r = RefractoryCounts
      emit_spike()
    end

    # set new input current
    I_stim = currents

  end

end

/*
Name: hh_cond_exp_traub - Hodgin Huxley based model, Traub modified.

Description:

 hh_cond_exp_traub is an implementation of a modified Hodkin-Huxley model

 (1) Post-synaptic currents
 Incoming spike events induce a post-synaptic change of conductance modeled
 by an exponential function. The exponential function is normalized such that an
 event of weight 1.0 results in a peak current of 1 nS.

 (2) Spike Detection
 Spike detection is done by a combined threshold-and-local-maximum search: if
 there is a local maximum above a certain threshold of the membrane potential,
 it is considered a spike.

Problems/Todo:
Only the channel variables m,h,n are implemented. The original
contains variables called y,s,r,q and \chi.
References:

Traub, R.D. and Miles, R. (1991) Neuronal Networks of the Hippocampus.
Cambridge University Press, Cambridge UK.

Sends: SpikeEvent

Receives: SpikeEvent, CurrentEvent, DataLoggingRequest

Author: Schrader

SeeAlso: hh_psc_alpha
*/
neuron hh_cond_exp_traub_implicit:

  state:
    V_m mV = E_L #  Membrane potential

    g_in nS = 0nS # Inhibitory synaptic conductance
    g_ex nS = 0nS # Excitatory synaptic conductance

    function alpha_n_init 1/ms = 0.032/(ms* mV ) * ( 15. mV - V_m) / ( exp( ( 15. mV - V_m) / 5. mV ) - 1. )
    function beta_n_init 1/ms = 0.5 /ms * exp( ( 10. mV - V_m ) / 40. mV )
    function alpha_m_init 1/ms = 0.32/(ms* mV ) * ( 13. mV - V_m) / ( exp( ( 13. mV - V_m) / 4. mV ) - 1. )
    function beta_m_init 1/ms = 0.28/(ms* mV ) * ( V_m  - 40. mV ) / ( exp( ( V_m - 40. mV ) / 5. mV ) - 1. )
    function alpha_h_init 1/ms = 0.128/ms * exp( ( 17. mV - V_m) / 18. mV )
    function beta_h_init 1/ms = ( 4. / ( 1. + exp( ( 40. mV - V_m ) / 5. mV) ) ) / ms

    Act_m real =  alpha_m_init / ( alpha_m_init + beta_m_init )
    Act_h real = alpha_h_init / ( alpha_h_init + beta_h_init )
    Inact_n real =  alpha_n_init / ( alpha_n_init + beta_n_init )
  end

  equations:
    # Add aliases to simplify the equation definition of V_m
    function I_Na  pA = g_Na * Act_m * Act_m * Act_m * Act_h * ( V_m - E_Na )
    function I_K   pA  = g_K * Inact_n * Inact_n * Inact_n * Inact_n * ( V_m - E_K )
    function I_L   pA = g_L * ( V_m - E_L )
    function I_syn_exc pA = cond_sum(g_ex, spikeExc) * ( V_m - E_ex )
    function I_syn_inh pA = cond_sum(g_in, spikeInh) * ( V_m - E_in )

    V_m' =( -I_Na - I_K - I_L - I_syn_exc - I_syn_inh + I_stim + I_e ) / C_m

    # channel dynamics
    function V_rel mV = V_m - V_T
    function alpha_n 1/ms = 0.032/(ms* mV ) * ( 15. mV - V_rel) / ( exp( ( 15. mV - V_rel) / 5. mV ) - 1. )
    function beta_n 1/ms = 0.5 /ms * exp( ( 10. mV - V_rel ) / 40. mV )
    function alpha_m 1/ms = 0.32/(ms* mV ) * ( 13. mV - V_rel) / ( exp( ( 13. mV - V_rel) / 4. mV ) - 1. )
    function beta_m 1/ms = 0.28/(ms* mV ) * ( V_rel  - 40. mV ) / ( exp( ( V_rel - 40. mV ) / 5. mV ) - 1. )
    function alpha_h 1/ms = 0.128/ms * exp( ( 17. mV - V_rel) / 18. mV )
    function beta_h 1/ms = ( 4. / ( 1. + exp( ( 40. mV - V_rel ) / 5. mV) ) ) / ms

    Act_m' = ( alpha_m - ( alpha_m + beta_m ) * Act_m )
    Act_h' = ( alpha_h - ( alpha_h + beta_h ) * Act_h )
    Inact_n' = ( alpha_n - ( alpha_n + beta_n ) * Inact_n )

    # synapses: exponential conductance
    g_ex' = -g_ex / tau_syn_ex
    g_in' = -g_in / tau_syn_in
  end

  parameters:
    g_Na nS = 20000.0nS       # Threshold Potential in mV
    g_K nS = 6000.0nS         # K Conductance
    g_L nS = 10nS             # Leak Conductance
    C_m pF = 200.0pF          # Membrane Capacitance in pF
    E_Na mV = 50mV            # Reversal potentials
    E_K mV = -90.mV           # Potassium reversal potential
    E_L mV = -60.mV           # Leak reversal Potential (aka resting potential) in mV
    V_T mV = -63.0mV          # Voltage offset that controls dynamics. For default
                              # parameters, V_T = -63mV results in a threshold around -50mV.
    tau_syn_ex ms = 5.0ms     # Synaptic Time Constant Excitatory Synapse in ms
    tau_syn_in ms = 10.0ms    # Synaptic Time Constant for Inhibitory Synapse in ms
    I_e pA = 0pA              # Constant Current in pA
    E_ex mV = 0.0 mV          # Excitatory synaptic reversal potential
    E_in mV = -80.0mV         # Inhibitory synaptic reversal potential
    I_stim pA = 0pA           # External input current
  end

  internals:
    RefractoryCounts integer = 20
    r integer # counts number of tick during the refractory period
  end

  input:
      spikeInh   <- inhibitory spike
      spikeExc   <- excitatory spike
      currents <- current
  end

  output: spike

  update:
    U_old mV = V_m
    integrate_odes()

    # sending spikes: crossing 0 mV, pseudo-refractoriness and local maximum...
    if r > 0:
      r -= 1
    elif V_m > V_T + 30mV and U_old > V_m:
      r = RefractoryCounts
      emit_spike()
    end

    # set new input current
    I_stim = currents

    g_ex += spikeExc * nS
    g_in += spikeInh * nS
  end

end