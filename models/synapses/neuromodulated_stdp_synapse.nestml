# neuromodulated_stdp - Synapse model for spike-timing dependent plasticity modulated by a neurotransmitter such as dopamine
# ##########################################################################################################################
#
# Description
# +++++++++++
#
# stdp_dopamine_synapse is a connection to create synapses with
# dopamine-modulated spike-timing dependent plasticity (used as a
# benchmark model in [1]_, based on [2]_). The dopaminergic signal is a
# low-pass filtered version of the spike rate of a user-specific pool
# of neurons. The spikes emitted by the pool of dopamine neurons are
# delivered to the synapse via the assigned volume transmitter. The
# dopaminergic dynamics is calculated in the synapse itself.
#
# Note that in this particular STDP synapse model, the weight is not allowed to be negative. In case an inhibitory STDP synapse needs to be modeled, this model (with weight >= 0 at all times) can be connected to a postsynaptic neuron at its appropriate (inhibitory) input port. The sign of the postsynaptic response is thus handled in the postsynaptic neuron. In principle, an STDP synapse model can be defined that allows for negative weights, but in this case, care should be taken to prevent the sign of the weight from changing during learning, as a biological synapse cannot simply switch from one type to another, say, from glutamatergic to GABAergic.
#
#
# References
# ++++++++++
#
# .. [1] Potjans W, Morrison A, Diesmann M (2010). Enabling functional neural
#        circuit simulations with distributed computing of neuromodulated
#        plasticity. Frontiers in Computational Neuroscience, 4:141.
#        DOI: https://doi.org/10.3389/fncom.2010.00141
# .. [2] Izhikevich EM (2007). Solving the distal reward problem through linkage
#        of STDP and dopamine signaling. Cerebral Cortex, 17(10):2443-2452.
#        DOI: https://doi.org/10.1093/cercor/bhl152
#
# Copyright statement
# +++++++++++++++++++
#
# This file is part of NEST.
#
# Copyright (C) 2004 The NEST Initiative
#
# NEST is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 2 of the License, or
# (at your option) any later version.
#
# NEST is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with NEST.  If not, see <http://www.gnu.org/licenses/>.
#
model neuromodulated_stdp_synapse:
    state:
        w real = 1. [[w >= 0]]
        n real = 0.   # Neuromodulator concentration
        c real = 0.   # Eligibility trace
        pre_tr real = 0.
        post_tr real = 0.

    parameters:
        tau_tr_pre ms = 20 ms    # STDP time constant for weight changes caused by pre-before-post spike pairings.
        tau_tr_post ms = 20 ms    # STDP time constant for weight changes caused by post-before-pre spike pairings.
        tau_c ms = 1000 ms    # Time constant of eligibility trace
        tau_n ms = 200 ms   # Time constant of dopaminergic trace
        b real = 0.   # Dopaminergic baseline concentration
        Wmax real = 200. [[Wmax >= 0]]    # Maximal synaptic weight
        Wmin real = 0. [[Wmin >= 0]]    # Minimal synaptic weight
        A_plus real = 1.    # Multiplier applied to weight changes caused by pre-before-post spike pairings. If b (dopamine baseline concentration) is zero, then A_plus is simply the multiplier for facilitation (as in the stdp_synapse model). If b is not zero, then A_plus will be the multiplier for facilitation only if n - b is positive, where n is the instantenous dopamine concentration in the volume transmitter. If n - b is negative, A_plus will be the multiplier for depression.
        A_minus real = 1.5    # Multiplier applied to weight changes caused by post-before-pre spike pairings. If b (dopamine baseline concentration) is zero, then A_minus is simply the multiplier for depression (as in the stdp_synapse model). If b is not zero, then A_minus will be the multiplier for depression only if n - b is positive, where n is the instantenous dopamine concentration in the volume transmitter. If n - b is negative, A_minus will be the multiplier for facilitation.

    equations:
        pre_tr' = -pre_tr / tau_tr_pre
        post_tr' = -post_tr / tau_tr_post

    internals:
        tau_s 1/ms = (tau_c + tau_n) / (tau_c * tau_n)

    input:
        pre_spikes <- spike
        post_spikes <- spike
        mod_spikes <- spike

    output:
        spike

    onReceive(mod_spikes):
        n += 1. / tau_n

    onReceive(post_spikes):
        post_tr += 1.

        # facilitation
        c += A_plus * pre_tr

    onReceive(pre_spikes):
        pre_tr += 1.

        # depression
        c -= A_minus * post_tr

        # deliver spike to postsynaptic partner
        emit_spike(w)

    # update from time t to t + timestep()
    update:
        # timestep() returns the timestep to be made (in units of time)
        # the sequence here matters: the update step for w requires the "old" values of c and n
        w -= c * (n / tau_s * expm1(-tau_s * timestep()) \
                 - b * tau_c * expm1(-timestep() / tau_c))
        c = c * exp(-timestep() / tau_c)
        n = n * exp(-timestep() / tau_n)

        integrate_odes(pre_tr, post_tr)
