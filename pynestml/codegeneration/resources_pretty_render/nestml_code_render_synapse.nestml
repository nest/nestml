model stdp_synapse:

    state:
        w real = 1              # Synaptic weight
        pre_trace real = 0
        post_trace real = 0

    parameters:
        d ms = 1 ms             # Synaptic transmission delay
        lambda real = 0.01      # Learning rate
        tau_tr_pre ms = 20 ms
        tau_tr_post ms = 20 ms
        alpha real = 1
        mu_plus real = 1
        mu_minus real = 1

    equations:
        pre_trace' = -pre_trace / tau_tr_pre
        post_trace' = -post_trace / tau_tr_post

    input:
        pre_spikes <- spike
        post_spikes <- spike

    output:
        spike

    onReceive(post_spikes):
        post_trace += 1

        # potentiate synapse
        w_ real = w + lambda * (1 - w)**mu_plus * pre_trace
        w = min(1, w_)

    onReceive(pre_spikes):
        pre_trace += 1

        # depress synapse
        w_ real = w - alpha * lambda * w**mu_minus * post_trace
        w = max(0, w_)

        # deliver spike to postsynaptic partner
        emit_spike(w, d)

    update:
        integrate_odes()
