model iaf_psc_alpha_neuron:

    state:
        V_m mV = E_L
        refr_t ms = 0 ms    # Refractory period timer
        is_refractory boolean = false

    equations:
        kernel I_kernel = (e / tau_syn) * t * exp(-t / tau_syn)
        inline I_syn pA = convolve(I_kernel, spikes) * pA
        V_m' = -(V_m - E_L) / tau_m + (I_syn + I_e + I_stim) / C_m

    parameters:
        C_m     pF = 250 pF       # Capacitance of the membrane
        tau_m   ms = 10 ms        # Membrane time constant
        tau_syn ms = 2 ms         # Time constant of synaptic current
        refr_T  ms = 2 ms         # Duration of refractory period
        E_L     mV = -70 mV       # Resting potential
        V_reset mV = -70 mV       # Reset potential of the membrane
        V_th    mV = -55 mV       # Spike threshold potential
        I_e     pA = 0 pA         # Constant external input current

    input:
        spikes <- spike
        I_stim pA <- continuous

    output:
        spike

    update:
        if is_refractory:
            # neuron is absolute refractory, do not evolve V_m
            refr_t -= resolution()
        else:
            # neuron not refractory, so evolve all ODEs (including V_m)
            integrate_odes()

    onCondition(V_m >= V_th):
        # threshold crossing
        refr_t = refr_T    # start of the refractory period
        is_refractory = true
        V_m = V_reset
        emit_spike()

    onCondition(is_refractory and refr_t <= resolution() / 2):
        # end of refractory period
        refr_t = 0 ms
        is_refractory = false
