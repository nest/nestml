neuron izhikevich_tutorial:

  initial_values:
    v mV = -65 mV    # Membrane potential in mV
    u real = 0    # Membrane potential recovery variable
  end

  equations:
    v' = (.04 * v * v / mV + 5 * v + (140 - u) * mV + (I_e * GOhm)) / ms
    u' = a * (b * v - u * mV) / (mV * ms)
  end

  parameters:
    a real = .02    # describes time scale of recovery variable
    b real = .2    # sensitivity of recovery variable
    c mV = -65 mV    # after-spike reset value of v
    d real = 8.    # after-spike reset value of u
  end

  input:
    spikes mV <- spike
    I_e pA <- current
  end

  output: spike

  update:
    integrate_odes()

    # add synaptic current
    v += spikes

    # threshold crossing
    if v >= 30mV:
      v = c
      u += d
      emit_spike()
    end

  end

end
